
plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.google.protobuf" version "0.9.6"
    id 'org.jreleaser' version '1.21.0'
}

group = 'com.bedatadriven'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.protobuf:protobuf-java:4.28.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.28.2"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.bedatadriven'
            artifactId = 'hrana-client'
            version '0.1'

            from components.java

            pom {
                name = 'hrana-client'
                description = 'Hrana client for Java'
                url = 'https://github.com/akbertram/hrana-client-java'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'akbertram'
                        name = 'Alex Bertram'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/akbertram/hrana-client-java.git'
                    developerConnection = 'scm:git:ssh://github.com/akbertram/hrana-client-java.git'
                    url = 'https://github.com/akbertram/hrana-client-java'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
